buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath group: 'org.flywaydb', name: 'flyway-database-postgresql', version: '10.2.0'
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.0'
	id 'io.spring.dependency-management' version '1.1.3'
	id "org.flywaydb.flyway" version "10.2.0"
	id "com.diffplug.spotless" version "6.24.0"
}

group = 'com.rohanc'
version = '1.0.3-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

compileJava {
	options.compilerArgs += [
		'-Amapstruct.defaultComponentModel=spring'
	]
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

// TODO: fix the tag
tasks.named("bootBuildImage") {
	imageName = "bmon-odds/server:1.0.3"
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.session:spring-session-core'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.2'
	testImplementation group: 'com.h2database', name: 'h2', version: '2.2.224'

	compileOnly group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '6.0.0'

	implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.0'
	implementation group: 'org.flywaydb', name: 'flyway-core', version: '10.2.0'
	runtimeOnly group: 'org.flywaydb', name: 'flyway-database-postgresql', version: '10.2.0'

	implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.2.20'
	implementation group: 'jakarta.validation', name: 'jakarta.validation-api', version: '3.0.2'
	implementation group: 'org.openapitools', name: 'jackson-databind-nullable', version: '0.2.6'
	implementation group: 'com.diffplug.spotless', name: 'spotless-plugin-gradle', version: '6.24.0'
	implementation group: 'com.google.googlejavaformat', name: 'google-java-format', version: '1.19.2'
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'

	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
	implementation "org.mapstruct.extensions.spring:mapstruct-spring-annotations:1.1.1"
	annotationProcessor "org.mapstruct.extensions.spring:mapstruct-spring-extensions:1.1.1"
	testAnnotationProcessor "org.mapstruct.extensions.spring:mapstruct-spring-extensions:1.1.1"
}

tasks.named('test') {
	useJUnitPlatform()
}

flyway {
	url =  System.getenv('POSTGRES_URL') + "/" + System.getenv('POSTGRES_DB')
	user = System.getenv('POSTGRES_USER')
	password = System.getenv('POSTGRES_PASS')
	defaultSchema = System.getenv('FLYWAY_SCHEMA')
	schemas = [System.getenv('FLYWAY_SCHEMA'), System.getenv('POSTGRES_SCHEMA')]
}

spotless {
		format 'misc', {
		target '*.gradle', '.gitattributes', '.gitignore'

		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}

	java {
		googleJavaFormat('1.19.2').skipJavadocFormatting()
		targetExclude("**/build/")
		importOrder()
		removeUnusedImports()
		formatAnnotations()
		licenseHeader '/* (C)$YEAR */'
	}
}
